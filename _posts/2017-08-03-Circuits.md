---
title: Circuits
tags: arduino, processing
layout: post
---

> Buy an Arduino Starter Kit.
> Get drunk and die for a day.
> Wake up to learn how to know.
> Connect devices with wires.
> Write the code to run on it.
> Drink more coffee, wine or beer.
> Start hacking, share you ideas.

![arduino](/img/drawing.png)

<!-- more -->

```java
// pin connection & serial codes
const int LEFT    = 2;
const int RED     = 3;
const int YELLOW  = 4;
const int GREEN   = 5;
const int BUZZ    = 8;
const int TONE    = 440;
const int BLUE    = 11;
const int RIGHT   = 12;

void setup() {
  Serial.begin(9600);
  pinMode(LEFT,   INPUT);
  pinMode(RIGHT,  INPUT);
  pinMode(RED,    OUTPUT);
  pinMode(YELLOW, OUTPUT);
  pinMode(GREEN,  OUTPUT);
  pinMode(BLUE,   OUTPUT);
  pinMode(BUZZ,   OUTPUT);
}

// hardware status
boolean left    = false;
boolean right   = false;
boolean red     = false;
boolean yellow  = false;
boolean green   = false;
boolean blue    = false;
boolean buzz    = false;
// time event
unsigned long check = 0;

void loop() {

  // incoming command
  if (Serial.available() > 0) {
    check = millis();
    int val = Serial.read();
    switch (val) {
      case RED:
        red = true;
        digitalWrite(RED, HIGH);
        break;
      case YELLOW:
        yellow = true;
        digitalWrite(YELLOW, HIGH);
        break;
      case GREEN:
        green = true;
        digitalWrite(GREEN, HIGH);
        break;
      case BLUE:
        blue = true;
        digitalWrite(BLUE, HIGH);
        break;
      case BUZZ:
        buzz = true;
        tone(BUZZ, TONE);
      default:
        break;
    }
  }

  // hardware indicators reset
  if (millis() - check > 666) {
    if (red) {
      red = false;
      digitalWrite(RED, LOW);
    }
    if (yellow) {
      yellow = false;
      digitalWrite(YELLOW, LOW);
    }
    if (green) {
      green = false;
      digitalWrite(GREEN, LOW);
    }
    if (blue) {
      blue = false;
      digitalWrite(BLUE, LOW);
    }
    if (buzz) {
      buzz = false;
      noTone(BUZZ);
    }
  }

  // move one step to left
  if (digitalRead(LEFT) == HIGH) {
    if (!left) {
      left = true;
      Serial.write(LEFT);
    }
  } else {
    left = false;
  }

  // move one step to right
  if (digitalRead(RIGHT) == HIGH) {
    if (!right) {
      right = true;
      Serial.write(RIGHT);
    }
  } else {
    right = false;
  }

}
```

Don't like the games. But love the code. Don't know the electronics. But like to learn new things. Remember the good old times with *Gameboy* in your pocket. The ugly gray plastic computer with liquid crystal display and 999 games all the same. And now, a half life later opening the designe precise box with a bit of resistors, wires and not enough big breadboard.

```java
import processing.serial.*;

Serial port;

void setup() {
  size(960, 960);
  //println((Object) Serial.list());
  port = new Serial(this, Serial.list()[0], 9600);
}

// serial codes for pins
final int left    = 2;
final int right   = 12;
final int red     = 3;
final int yellow  = 4;
final int green   = 5;
final int blue    = 11;
final int buzz    = 8;

final int[] diods = {red, yellow, green, blue};  // le diods colors
final int[] coins = {5, 10, 15, -20};            // le diods prices
final color[] colors = {
  color(255, 000, 000), // red
  color(255, 255, 000), // yellow
  color(000, 255, 000), // green
  color(000, 000, 255), // blue
};

// initial condition
int x0     = 0;
int y0     = 800;
int size   = 60;
int step   = 120;
int score  = 0;

class LED {
  int s = 0;  // speed
  int y = 0;
  int x = step * floor(random(-3, 4));
  int c = int(random(4));  // color

  void step() {
    y += s + 5;
  }

  void show() {
    noStroke();
    fill(colors[c]);
    ellipse(x, y, size, size);
  }
}

ArrayList<LED> leds = new ArrayList<LED>();

void draw() {

  background(192);
  translate(width/2, 0);

  // incoming command
  if (port.available() > 0) {
    int move = port.read();
    if (move == left) {
      if (x0 == -3 * step) {
        port.write(buzz);
      } else {
        x0 -= step;
      }
    }
    if (move == right) {
      if (x0 == 3 * step) {
        port.write(buzz);
      } else {
        x0 += step;
      }
    }
  }

  // add one LED per second
  if (frameCount % 60 == 1) leds.add(new LED());

  // manage LEDs on circuit
  for (int i = 0; i < leds.size(); i++) {
    LED led = leds.get(i);
    led.s = int(score / 100);  // score to speed up
    led.step();
    if (led.y > height) {
      leds.remove(i);
    } else {
      led.show();
    }
    if (led.x == x0 && abs(led.y - y0) < size/2) {
      score += coins[led.c];
      port.write(diods[led.c]);
      leds.remove(i);
    }
  }

  // show player
  stroke(0);
  strokeWeight(2);
  fill(255, 127);
  ellipse(x0, y0, size, size);

  // show score
  fill(255, 96);
  textSize(32);
  text(score, 15-width/2, height-25);
}
```

Â¶ [Circuits](https://github.com/astrograzl/Circuits)
